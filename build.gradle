plugins {
    id 'java'
    id 'maven-publish'
    id 'distribution'
    id 'jacoco'
    id "com.github.kt3k.coveralls" version "2.6.3"
}

group = 'tk.hongkailiu.mytool'
version = '0.1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.7'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.1.0'
    compile group: 'org.mockito', name: 'mockito-core', version: '2.6.4'

    //compile group: 'args4j', name: 'args4j', version: '2.33'
    compile group: 'com.beust', name: 'jcommander', version: '1.60'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.12'

    testCompile 'junit:junit:4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}


task createVersionFile(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version").withWriter { w ->
            w.writeLine project.version.toString()
        }
    }
}
classes.dependsOn(createVersionFile)

jar {
    manifest {
        attributes(
                "Main-Class": "tk.hongkailiu.mytool.MyTool",
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}


task srcZip(type: Zip) {
    classifier = 'src'
    from projectDir
    include 'src/**/*', 'bin'
}

distributions {
    main {
        contents {
            into('libs') {
                from jar, configurations.runtime

            }
            into('bin') {
                from 'bin'
            }
            into('conf') {
                from 'src/main/resources'
            }
        }
    }
}
distTar.enabled = false

publishing {
    publications {
        myPublicationName(MavenPublication) {
            artifact distZip
            artifact srcZip
            artifact jar
        }
    }
}
publish.dependsOn(srcZip)

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled = true
        html.destination "${buildDir}/jacocoHtml"
    }
}
